C--**--CH175--524--P:CAP--25:5:1999
C--**--CH154--524--A:H--20:5:1999
C--**--CH153--524--U:D--20:5:1999
C ABCDEFGHIJKLMNOPQRSTUVWXYZ$0123456789+-*/=(),.
C
C                        MP  (VERSION 780802)
C                        ********************
C
C $$                   ******  COMMENTS  ******
C
C MP IS A MULTIPLE-PRECISION FLOATING-POINT ARITHMETIC PACKAGE.
C IT IS ALMOST COMPLETELY MACHINE-INDEPENDENT, AND SHOULD
C RUN ON ANY MACHINE WITH AN ANSI STANDARD FORTRAN COMPILER,
C SUFFICIENT MEMORY, AND A WORDLENGTH OF AT LEAST 16 BITS.
C
C FOR A GENERAL DESCRIPTION OF THE PHILOSOPHY AND DESIGN OF MP,
C SEE - R. P. BRENT, A FORTRAN MULTIPLE-PRECISION ARITHMETIC
C PACKAGE, ACM TRANS. MATH. SOFTWARE 4 (MARCH 1978), 57-70.
C SOME ADDITIONAL DETAILS ARE GIVEN IN THE SAME ISSUE, 71-81.
C FOR DETAILS OF THE IMPLEMENTATION, CALLING SEQUENCES ETC. SEE
C THE MP USERS GUIDE.
C
C MP IS NORMALLY DISTRIBUTED IN FIVE FILES.  ALL HAVE 80 CHARACTER
C LOGICAL RECORDS AND USE ONLY THE (STANDARD FORTRAN) CHARACTERS
C APPEARING IN LINE MP000011.
C
C FILE 1 - THESE COMMENTS AND EXAMPLE PROGRAM.
C FILE 2 - MP SUBROUTINES (EXCLUDING EXAMPLE AND TEST PROGRAMS).
C FILE 3 - TEST PROGRAMS (NOT USING AUGMENT INTERFACE).
C FILE 4 - MP USERS GUIDE.
C FILE 5 - AUGMENT DESCRIPTION DECK AND JACOBI PROGRAM USING IT.
C          (MP MAY BE USED WITH THE AUGMENT PREPROCESSOR.  FOR
C           DETAILS SEE SECTION 4 OF THE USERS GUIDE.)
C
C TO INSTALL MP, READ THESE FIVE FILES.  PRINT FILE 4 (THE USERS GUIDE)
C USING THE FIRST CHARACTER (BLANK, 0 OR 1) AS STANDARD FORTRAN
C PRINTER CONTROL.  THEN FOLLOW THE INSTRUCTIONS GIVEN IN SECTION 5
C OF THE USERS GUIDE.
C
C
C
C $$                   ******  EXAMPLE  ******
C
C THIS PROGRAM COMPUTES PI AND EXP(PI*SQRT(163/9)) TO 100
C DECIMAL PLACES, AND EXP(PI*SQRT(163)) TO 90 DECIMAL PLACES,
C AND WRITES THEM ON LOGICAL UNIT 6.  EXECUTION
C TIME ON A UNIVAC 1108 (WITH FORTRAN SE1D) IS 1.051 SECONDS.
C
C TO RUN EXAMPLE THE FOLLOWING MP ROUTINES ARE REQUIRED - MPABS,
C MPADD, MPADDI, MPADD2, MPADD3, MPART1, MPCHK, MPCIM, MPCLR, MPCMF,
C MPCMI, MPCMPR, MPCMR, MPCOMP, MPCQM, MPCRM, MPDIVI, MPERR,
C MPEXP, MPEXP1, MPGCD, MPLNI, MPL235, MPMAXR, MPMLP, MPMUL,
C MPMULI, MPMULQ, MPMUL2, MPNZR, MPOUT, MPOUT2, MPOVFL, MPPI,
C MPPWR, MPQPWR, MPREC, MPROOT, MPSET, MPSTR, MPSUB, MPUNFL.
C
C CORRECT OUTPUT (EXCLUDING HEADINGS) IS AS FOLLOWS
C
C                  3.14159265358979323846264338327950288419716939937510
C                    58209749445923078164062862089986280348253421170680
C             640320.00000000060486373504901603947174181881853947577148
C                    57603665918194652218258286942536340815822646477590
C 262537412640768743.99999999999925007259719818568887935385633733699086
C                    2707537410378210647910118607312951181346
C
C CERTAIN PARAMETERS AND WORKING SPACE IN COMMON.
C
C MPEXP REQUIRES 4T+10 WORDS AND WE HAVE T .LE. 62 IF WORDLENGTH
C AT LEAST 16 BITS, SO 4T+10 .LE. 258.  DIMENSIONS CAN BE REDUCED
C IF WORDLENGTH IS GREATER THAN 16 BITS.
C
C VARIABLES NEED T+2 .LE. 64 WORDS AND ALLOW 110 CHARACTERS FOR
C DECIMAL OUTPUT
C
C CALL MPSET TO SET OUTPUT LOGICAL UNIT = 6 AND EQUIVALENT
C NUMBER OF DECIMAL PLACES TO AT LEAST 110.  THE LAST TWO
C PARAMETERS ARE THE DIMENSIONS OF PI (OR X) AND R.
C     .. Parameters ..
      INTEGER RMAX
      PARAMETER (RMAX=15000)
C     ..
C     .. Scalars in Common ..
      INTEGER B,LUN,M,MXR,T
C     ..
C     .. Arrays in Common ..
      INTEGER R(RMAX)
C     ..
C     .. Local Arrays ..
      INTEGER C(110),PI(64),X(64)
C     ..
C     .. External Subroutines ..
      EXTERNAL MPEXP,MPMUL,MPOUT,MPPI,MPPWR,MPQPWR,MPSET
C     ..
C     .. Common blocks ..
      COMMON B,T,M,LUN,MXR,R
C     ..
      CALL MPSET(6,110,64,258)
C
C COMPUTE MULTIPLE-PRECISION PI
      CALL MPPI(PI)
C
C CONVERT TO PRINTABLE FORMAT (F110.100) AND WRITE
      CALL MPOUT(PI,C,110,100)
      WRITE (LUN,FMT=9000) B,T,C
C
C SET X = SQRT(163/9), THEN MULTIPLY BY PI
      CALL MPQPWR(163,9,1,2,X)
      CALL MPMUL(X,PI,X)
C
C SET X = EXP(X)
      CALL MPEXP(X,X)
C
C CONVERT TO PRINTABLE FORMAT AND WRITE
      CALL MPOUT(X,C,110,100)
      WRITE (LUN,FMT=9010) C
C
C SET X = X**3 = EXP(PI*SQRT(163))
      CALL MPPWR(X,3,X)
C
C WRITE IN FORMAT F110.90
      CALL MPOUT(X,C,110,90)
      WRITE (LUN,FMT=9020) C
      STOP

 9000 FORMAT ('1EXAMPLE OF MP PACKAGE,   BASE =',I9,',   DIGITS =',I4,
     +       ///' PI TO 100D',//11X,60A1,/21X,50A1)
 9010 FORMAT (/' EXP(PI*SQRT(163/9)) TO 100D',//11X,60A1,/21X,50A1)
 9020 FORMAT (/' EXP(PI*SQRT(163)) TO 90D',//1X,70A1,/21X,40A1)
      END
