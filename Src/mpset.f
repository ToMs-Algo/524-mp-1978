C--**--CH180--524--P:CAP--26:5:1999
C--**--CH163--524--A:H--20:5:1999
C--**--CH162--524--C:D--20:5:1999
C--**--CH161--524--U:D--20:5:1999
      SUBROUTINE MPSET(LUNIT,IDECPL,ITMAX2,MAXDR)
C SETS BASE (B) AND NUMBER OF DIGITS (T) TO GIVE THE
C EQUIVALENT OF AT LEAST IDECPL DECIMAL DIGITS.
C IDECPL SHOULD BE POSITIVE.
C ITMAX2 IS THE DIMENSION OF ARRAYS USED FOR MP NUMBERS,
C SO AN ERROR OCCURS IF THE COMPUTED T EXCEEDS ITMAX2 - 2.
C MPSET ALSO SETS
C       LUN = LUNIT (LOGICAL UNIT FOR ERROR MESSAGES),
C       MXR = MAXDR (DIMENSION OF R IN COMMON, .GE. T+4), AND
C         M = (W-1)/4 (MAXIMUM ALLOWABLE EXPONENT),
C WHERE W IS THE LARGEST INTEGER OF THE FORM 2**K-1 WHICH IS
C REPRESENTABLE IN THE MACHINE, K .LE. 47
C THE COMPUTED B AND T SATISFY THE CONDITIONS
C (T-1)*LN(B)/LN(10) .GE. IDECPL   AND   8*B*B-1 .LE. W .
C APPROXIMATELY MINIMAL T AND MAXIMAL B SATISFYING
C THESE CONDITIONS ARE CHOSEN.
C PARAMETERS LUNIT, IDECPL, ITMAX2 AND MAXDR ARE INTEGERS.
C BEWARE - MPSET WILL CAUSE AN INTEGER OVERFLOW TO OCCUR
C ******   IF WORDLENGTH IS LESS THAN 48 BITS.
C          IF THIS IS NOT ALLOWABLE, CHANGE THE DETERMINATION
C          OF W (DO 30 ... TO 30 W = WN) OR SET B, T, M,
C          LUN AND MXR WITHOUT CALLING MPSET.
C FIRST SET LUN AND MXR
C     .. Parameters ..
      INTEGER RMAX
      PARAMETER (RMAX=15000)
C     ..
C     .. Scalar Arguments ..
      INTEGER IDECPL,ITMAX2,LUNIT,MAXDR
C     ..
C     .. Scalars in Common ..
      INTEGER B,LUN,M,MXR,T
C     ..
C     .. Arrays in Common ..
      INTEGER R(RMAX)
C     ..
C     .. Local Scalars ..
      INTEGER I,I2,K,W,W2,WN
C     ..
C     .. External Subroutines ..
      EXTERNAL MPCHK,MPERR
C     ..
C     .. Intrinsic Functions ..
      INTRINSIC ALOG,FLOAT,INT
C     ..
C     .. Common blocks ..
      COMMON B,T,M,LUN,MXR,R
C     ..
      LUN = LUNIT
      MXR = RMAX
C CHECK THAT LUN IN RANGE 1,...,99 AND WRITE ERROR MESSAGE
C ON UNIT 6 IF NOT.
      IF ((LUN.GT.0) .AND. (LUN.LT.100)) GO TO 10
      WRITE (6,FMT=9000) LUN
      LUN = 6
      CALL MPERR
      RETURN
C DETERMINE LARGE REPRESENTABLE INTEGER W OF FORM 2**K - 1
   10 W = 0
      K = 0
C ON CYBER 76 HAVE TO FIND K .LE. 47, SO ONLY LOOP
C 47 TIMES AT MOST.  IF GENUINE INTEGER WORDLENGTH
C EXCEEDS 47 BITS THIS RESTRICTION CAN BE RELAXED.
C ******* CHANGED 47 to 31 as this is far more common
C     DO 30 I = 1, 47
      DO 20 I = 1,31
C INTEGER OVERFLOW WILL EVENTUALLY OCCUR HERE
C IF WORDLENGTH .LT. 48 BITS
          W2 = W + W
          WN = W2 + 1
C APPARENTLY REDUNDANT TESTS MAY BE NECESSARY ON SOME
C MACHINES, DEPENDING ON HOW INTEGER OVERFLOW IS HANDLED
          IF ((WN.LE.W) .OR. (WN.LE.W2) .OR. (WN.LE.0)) GO TO 30
          K = I
          W = WN
   20 CONTINUE
C SET MAXIMUM EXPONENT TO (W-1)/4
   30 M = W/4
      IF (IDECPL.GT.0) GO TO 40
      WRITE (LUN,FMT=9010)
      CALL MPERR
      RETURN
C B IS THE LARGEST POWER OF 2 SUCH THAT (8*B*B-1) .LE. W
   40 B = 2** ((K-3)/2)
C 2E0 BELOW ENSURES AT LEAST ONE GUARD DIGIT
      T = INT(2E0+FLOAT(IDECPL)*ALOG(10E0)/ALOG(FLOAT(B)))
C SEE IF T TOO LARGE FOR DIMENSION STATEMENTS
      I2 = T + 2
      IF (I2.LE.ITMAX2) GO TO 50
      WRITE (LUN,FMT=9020) I2
      CALL MPERR
C REDUCE TO MAXIMUM ALLOWED BY DIMENSION STATEMENTS
      T = ITMAX2 - 2
C CHECK LEGALITY OF B, T, M, LUN AND MXR (AT LEAST T+4)
   50 CALL MPCHK(1,4)
      RETURN

 9000 FORMAT (' *** LUNIT =',I10,' ILLEGAL IN CALL TO MPSET ***')
 9010 FORMAT (' *** IDECPL .LE. 0 IN CALL TO MPSET ***')
 9020 FORMAT (' *** ITMAX2 TOO SMALL IN CALL TO MPSET ***',/' *** INCR',
     +       'EASE ITMAX2 AND DIMENSIONS OF MP ARRAYS',' TO AT LEAST',
     +       I10,' ***')
      END
      INTEGER FUNCTION MPSIGA(X)
C RETURNS SIGN OF MP NUMBER X
C     .. Array Arguments ..
      INTEGER X(1)
C     ..
      MPSIGA = X(1)
      RETURN

      END
